# Jones 3D Cog Script
#
# weap_IMP3.cog
#
# [RT]
#
# Controls Infernal Machine Part #3, the Levitator.
#
# (C) 1998 LucasArts Entertainment Company LLC. All Rights Reserved
#
# ===================================================================

symbols

message		startup
message		fire
message		activated
message		deactivated
message		selected
message		deselected
message		callback
message		pulse
message		changed

model		impOn=imp3_levit_on.3do		local	# Autoloaded
model		impOff=imp3_levit.3do		local	# Autoloaded

sound		pull=gen_grenade_pull.wav	local	# Autoloaded
sound		put=gen_grenade_put.wav		local	# Autoloaded
sound		pow=imp3_activate.wav		local	# Autoloaded
sound		on=imp3_powerup.wav			local	# Autoloaded
sound		off=imp3_powerdown.wav		local	# Autoloaded
sound		loop=imp3_on_loop.wav		local	# Autoloaded

template	tplGlow=+imp3_glow			local	# Autoloaded

thing		player						local
thing		glow						local

int			weaponNum=16				local
int			handMesh					local
int			handNode					local
int			callNum						local
int			curSound					local
int			index						local

int			bOn=0						local
int			bDisabled=0					local

vector		dustPos						local

end

# ===================================================================

code

startup:

	player = GetLocalPlayerThing();
	handMesh = GetMeshByName(player, "inrhand");
	handNode = GetNodeByName(player, "inrhand");

	return;

# -------------------------------------------------------------------

fire:

	if (bDisabled)
	{
		bDisabled = 0;
		return;
	}

	if (bOn)
	{
		SetThingMesh(player, handMesh, impOff, 0);
		IMPEndFiring(16);
		SetPulse(0);
        bOn = 0;
	}
	else
	{
		SetThingMesh(player, handMesh, impOn, 0);
		IMPStartFiring(16);

		PlaySoundThing(pow, player, 1, -1, -1, 0x880);
		StartJewelFlying();

		SetPulse(0.1);
		dustPos = GetThingJointPos(player, handNode);
		dustPos = VectorAdd(dustPos, '0 0 0.025'); 
		MakeFairyDust(player, dustPos);
        bOn = 1;
	}

	SetFireWait(player, 1.0);

	return;

# -------------------------------------------------------------------

activated:

	ActivateWeapon(player, 0.01);
	return;

# -------------------------------------------------------------------

deactivated:

	DeactivateWeapon(player);
	return;

# -------------------------------------------------------------------

selected:

	SetAimWait(player, 0.0);
	SetMountWait(player, 0.7);

	SetArmedMode(player, 5);
	SetCurWeapon(player, weaponNum);
	SetThingFireOffset(player, '0.0 0.0 0.0'); 

	CaptureThing(player);
	PlayMode(player, 66, 0);

	return;

# -------------------------------------------------------------------

deselected:

	if(bOn)
	{
		SetThingMesh(player, handMesh, impOff, 0);
		IMPEndFiring(16);
		SetPulse(0);
        bOn = 0;
	}

	SetMountWait(player, 1.0);
	StopMode(player, 66, 0);

	CaptureThing(player);
	PlayMode(player, 67, 0);

	SetArmedMode(player, 0);

	StopSound(curSound, 0.0);
	PlaySoundThing(off, player, 1, -1, -1, 0x880);

	return;

# -------------------------------------------------------------------

callback:

	callNum = GetParam(1);
	ReleaseThing(player);

	if (callNum == 28)
	{
		# Model swaps & FX
		SetWeaponModel(player, weaponNum);
		glow = AttachThingToThingMesh(player, tplGlow, handMesh);
		
		# Sounds
		PlaySoundThing(pull, player, 1, -1, -1, 0x880);
		PlaySoundThing(on, player, 1, -1, -1, 0x880);
		Sleep(0.2);
		curSound = PlaySoundThing(loop, player, 1, -1, -1, 0x881);
	}
	else if (callNum == 29)
	{
		# Cleanup...
		ResetWeaponModel(player);
		DetachThingMesh(glow);

		PlaySoundThing(put, player, 1, -1, -1, 0x880);
	}

	return;


# -------------------------------------------------------------------

pulse:

	dustPos = GetThingJointPos(player, handNode);
	dustPos = VectorAdd(dustPos, '0 0 0.025'); 
	MakeFairyDust(player, dustPos);
	return;

# -------------------------------------------------------------------

changed:

	# This message is called when an IMP is activated on an IMP stand
	bDisabled = 1;
	return;

end
