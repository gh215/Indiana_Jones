# Jones 3D Cog Script
#
# olv_exteriorambient.cog
#
# Plays random sounds at random locations
#
# [DS]
#
# (C) 1999 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols
	message	pulse
	message	startup
	message	entered

	thing		soundPos0		nolink
	thing		soundPos1		nolink
	thing		soundPos2		nolink
	thing		soundPos3		nolink
	thing		soundPos4		nolink
	thing		soundPos5		nolink
	thing		soundPos6		nolink
	thing		soundPos7		nolink

	sector	turn_on_sec
	sector	turn_off_sec
	int		on_startup=0				# set to 1 to start on startup.	

	sound		wav0=olv_outside_a01.wav		local
	sound		wav1=olv_outside_a02.wav		local
	sound		wav2=olv_outside_a03.wav		local
	sound		wav3=olv_outside_a04.wav		local
	sound		wav4=olv_outside_a05.wav		local
	sound		wav5=olv_outside_a06.wav		local
	sound		wav6=gen_sg_squawk.wav			local
	sound		wav7=olv_outside_a08.wav		local
	thing		player					local
	float		minDist=5				local
	float		maxDist=35				local
	float		volume=0.8				local
	int		sndnum				local
	int		numsounds=8				local
	int		numlocl=8				local		
	int		cnt					local

end

code

# ........................................................................................

startup:
	player=GetLocalPlayerThing();
	if(on_startup == 1)
	{
	setpulse(1.0);
	}
	return;

entered:
	if(getSenderRef() == turn_on_sec)
	{
	setpulse(1.0);
	}
	if(getSenderRef() == turn_off_sec)
	{
	setpulse(0.0);
	}
	return;


pulse:
	sndnum = rand()*numsounds;
	cnt = rand()*numlocl;
#	printint(sndnum);
	PlaySoundThing(wav0[sndnum], soundPos0[cnt], volume, minDist, maxDist, 0);
	return;


end