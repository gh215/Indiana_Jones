# Jones 3D Cog Script
#
# TEM_FogT2Shaft.cog
#
# [MDR] && [TRM]
#
# (C) 1999 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols

    message     entered
    message     pulse
    
    thing       player          local
    thing       fogGhost
    
    sector      tiki2Shaft
    sector      lavaRoom
                       
    vector      vec_facing      local
    
    int         inShaft=0        local
    
end

# ========================================================================================

code

entered:

    player = GetLocalPlayerThing();
    
    if(GetSenderRef() == tiki2Shaft)
    {
        Print("entered tiki2Shaft");
        inShaft = 1;
        SetPulse(0.5);
    }
    
    if(GetSenderRef() == lavaRoom)
    {
        Print("entered lavaRoom");
        inShaft = 0;
        SetPulse(0.0);
        
        # set fog to red
        SetFog(1, '0.5, 0.05 0.05', 50, 190);       # 70, 150
    }
        
    return;
        
# ========================================================================================

pulse:

    vec_facing = VectorSub( GetThingPos(fogGhost), GetThingPos(player));
    vec_facing = VectorNorm(vec_facing);
    
    if(inShaft == 1)
    {
        if (VectorDot(GetThingLVec(player), vec_facing) >= 0)
        {
            # Player is facing fogGhost
            # set fog to red
            #SetFog(1, '0.5, 0.05 0.05', 40, 125);
            SetFog(1, '0.5, 0.05 0.05', 50, 190);
        }
        
        else
        {
            # player's back is to fogGhost
            # change fog values to blue
            SetFog(1, '0.50 0.50 0.60', 10, 350);
        }
    }
    
    else
        return;
    
    return;
        
# ========================================================================================

end

