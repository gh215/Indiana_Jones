# Jones 3D Cog Script
#
# INF_FlyingRobot.cog
# Controls the robot sector thing
#
# [SXC]
#
# (C) 1998 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols

    message     startup
    message     timer
    message     entered
    message     crossed
    
    message     user0       # right imp part inserted
    message     user1       # wrong part inserted
    message     user2       # no part inserted
    
    cog         exitcog
    
    thing       cage_cam0
    thing       cage_tgt0
    
    thing       door_cam0
    thing       door_tgt0
    
    thing       scn_cam0
    thing       scn_tgt0
    
    thing       scn_cam1
    thing       scn_tgt1
    
    thing       player                                      local
    thing       actorindy
    thing       actorindy2
    thing       cage
    thing       cg_mk_2
    
    thing       octdoor0
    thing       octdoor1
    thing       corridoor
    thing       impelevator
    thing       impstand
    
    thing       whipstrut0
    thing       whipstrut1
    
    thing       generator_pos
    thing       horseman
    
    thing       m_voiceactor
    
    surface     croschek
    
    template    e_pool=EnergyWhirlpool                      local
    
    int         elevflag=0                                  local
    int         elevchan                                    local
    int         startchan                                   local
    int         movechan                                    local
    int         mardukchan                                  local
    int         crosit=0                                    local
    int         fx_channel                                  local
    int         curchan                                     local
    int         toggleint=0                                 local
    int         headkey                                     local
    int         headchannel                                 local
    int         voiceflag                                   local

    sound       sophialine0=Im03s18.wav                     local
    sound       gearplatmove=imp3_powerdown.wav             local
    sound       gearplatstart=imp4_chargeup.wav             local
    sound       gearplatstop=imp5_powerdown.wav             local
    
    sound       mardukline0=im10i01.wav                     local
    sound       mardukline1=im10i03.wav                     local
    sound       indyline0=im10j02.wav                       local
    sound       elevmusic=mus_inf_awesome4.wav              local
    sound       horse_headopen=inf_mr_headopen.wav          local
    
    sound       cagedn=mus_teo_statuerise.wav               local

    keyframe    in_scratch=0in_scrtch_1_1.key               local
    keyframe    head_on_anim=inf_robo_head_anim.key         local
    
    material    botmat=mrhdfr.mat                           local
    
end


code
#........................................................................................

startup:

    //inital conditions
    MoveToFrame(impelevator, 1, 5);
    AttachThingToThing(cage_tgt0, cage);
    player=GetLocalPlayerThing();
        
return;

#........................................................................................
//right part
user0:

    //print("The right part has been inserted");
    MoveToFrame(impelevator, 0, 10);
    //StartCutscene called in INF_ImpSwitcher
    
    //hold on particles for a moment
    Sleep(1);
    
    //cut to cage and send it down
    SetCameraLookInterp(2, 0);
    SetCameraPosInterp(2, 0);
    SetCameraInterpSpeed(2, 0);
    Sleep(0.001);
    
    SetCameraSecondaryFocus(2, cage_tgt0);
    SetCameraFocus(2, cage_cam0);
    SetCurrentCamera(2);
    SetCameraFOV(70, 0, 0);
    Sleep(1);
    
    AISetMoveSpeed(cage, 1);
	AISetMoveThing(cage, cg_mk_2, 0);
    fx_channel=PlaySoundThing(cagedn, cage, 1, 10, 20, 0x0080);
    Sleep(1);
    PlaySoundLocal(cagedn, 1, 0, 0, 0);

    //play sophia voiceline
    PlayVoice(cage, sophialine0, 1, 0);
	AIWaitForStop(cage);
    Sleep(1);
    
    //sets okaytofinish to 1 in INF_Cinematic_Exit
    SendMessage(exitcog, user7);
    
    //close the octodoors
    MoveToFrame(octdoor0, 1, 1);
    MoveToFrame(octdoor1, 1, 1);
    
    //cut to corridoor
    SetCameraSecondaryFocus(2, door_tgt0);
    SetCameraFocus(2, door_cam0);
    SetCurrentCamera(2);
    SetCameraFOV(70, 1, 2);
    
    //open up the corridoor
    MoveToFrame(corridoor, 1, 1);
    WaitForStop(corridoor);
    Sleep(1);
    
    //return control to player
    CopyOrientAndPos(actorindy, player);
    SetThingFlags(actorindy, 0x80000);
    ClearActorFlags(player, 0x200000);
    ClearThingFlags(player, 0x10);
    ClearThingFlags(player, 0x80000);
    SetCurrentCamera(1);
    SetCameraFOV(90, 0, 0);
    EndCutscene();
    
    //enable the elevator
    elevflag=1;
    
    //lower the whipstruts
    
    return;

#........................................................................................
//wrong part
user1:

    //print("This is the wrong part");
    
    CopyOrientAndPos(actorindy, player);
    SetThingFlags(actorindy, 0x80000);
    ClearActorFlags(player, 0x200000);
    ClearThingFlags(player, 0x10);
    ClearThingFlags(player, 0x80000);
    SetCurrentCamera(1);
    SetCameraFOV(90, 0, 0);
    EndCutscene();
        
return;

#........................................................................................
//no part
user2:

    //print("no current part");
    
    CopyOrientAndPos(actorindy, player);
    SetThingFlags(actorindy, 0x80000);
    ClearActorFlags(player, 0x200000);
    ClearThingFlags(player, 0x10);
    ClearThingFlags(player, 0x80000);
    SetCurrentCamera(1);
    SetCameraFOV(90, 0, 0);
    EndCutscene();    
    
return;

#........................................................................................
//elevator control
entered:

    if ((GetSenderRef() == impelevator) && (elevflag == 1))
    {
        elevflag=2;
        Rotate(whipstrut0, -90, 2, 3);
        Rotate(whipstrut1, -90, 2, 3);
        Sleep(0.5);
        startchan=PlaySoundThing(gearplatstart, impelevator, 1, 10, 20, 0x0080);
        WaitForSound(startchan);
        //music
		PlaySoundLocal(elevmusic, 1, 0, 0x0, 0);
        MoveToFrame(impelevator, 2, 5);
        movechan = PlaySoundThing(gearplatmove, impelevator, 1, 5, 10, 0x0081);
        WaitForStop(impelevator);
        StopSound(movechan, 0);
        PlaySoundThing(gearplatstop, impelevator, 1, 10, 20, 0x0080);
        Rotate(whipstrut0, 90, 2, 3);
        Rotate(whipstrut1, 90, 2, 3);
    }
    
return;
        
#........................................................................................
// small cutscene introduced the marduk head
crossed:

    If ((GetSenderRef() == croschek) && (crosit == 0))
    {
        //start cutscene
        crosit=1;
        SetActorFlags(player, 0x200000);
        MakeMeStop();
        DeselectWeaponWait(player);
        StartCutscene(1);
        CopyPlayerHolsters(player, actorindy2);
        SetThingFlags(player, 0x80000);
        ClearThingFlags(actorindy2, 0x80000);
        
        //Prep actor
        AISetCutsceneMode(actorindy2);
        SetCameraFOV(90, 0, 0);
        
        //Cut to Marduk head
        SetCameraFocus(2, scn_cam0);
        SetCameraSecondaryFocus(2, scn_tgt0);
        SetCurrentCamera(2);
        headchannel=PlaySoundThing(horse_headopen, horseman, 0.75, 10, 20, 0x0080);
        headkey=PlayKey(horseman, head_on_anim, 4, 0x14, 0);
        SetCameraFOV(80, 0, 0);
        Sleep(0.01);
        SetCameraFOV(50, 1, 6);
        curchan = PlayVoice(m_voiceactor, mardukline0, 1, 0);
        SetTimer(0.1);
        voiceflag=0;
        WaitforSound(curchan);
        voiceflag=1;
        
        //Cut to Indy
        SetCameraFocus(2, scn_cam1);
        SetCameraSecondaryFocus(2, scn_tgt1);
        SetCurrentCamera(2);
        SetCameraFOV(80, 0, 0);
        Sleep(0.01);
        SetCameraFOV(60, 1, 3.5);
        PlayKey(actorindy2, in_scratch, 4, 0x12, 0);
        PlayVoice(actorindy2, indyline0, 1, 1); 
        Sleep(1);
        
        //Cut back to Marduk head
        SetCameraFocus(2, scn_cam0);
        SetCameraSecondaryFocus(2, scn_tgt0);
        SetCurrentCamera(2);
        SetCameraFOV(60, 0, 0);
        Sleep(0.01);
        SetCameraFOV(37, 1, 5.5);
        curchan = PlayVoice(m_voiceactor, mardukline1, 1, 0);
        SetTimer(0.1);
        voiceflag=0;
        WaitForSound(curchan);
        voiceflag=1;
        SetMaterialCel(botmat, 0);
        StopKey(horseman, headkey, 1);
        headchannel=PlaySoundThing(horse_headopen, horseman, 0.75, 10, 20, 0x0080);
        Sleep(2);
        
        //restore everything
        CopyOrientAndPos(actorindy2, player);
        SetCameraLookInterp(2, 0);
        SetCameraPosInterp(2, 0);
        SetCameraInterpSpeed(2, 0);
        Sleep(0.01);
        ClearThingFlags(player, 0x80000);
	    SetThingFlags(actorindy2, 0x80000);
        SetCurrentCamera(1);
        SetCameraFOV(90, 0, 0);
        ResetCameraFOV(0, 0);
        Sleep(1);
        EndCutscene();
        ClearActorFlags(player, 0x200000);
        
    }
    
return;

#........................................................................................
timer:

    toggleint=1-toggleint;
    if (voiceflag == 1)
    {
        return;
    }
    
    if(toggleint == 0)
    {
        SetMaterialCel(botmat, 1);
        SetTimer(RandBetween(1, 2)*0.05);
        return;
    }
    
    if(toggleint == 1)
    {
        SetMaterialCel(botmat, 0);
        SetTimer(RandBetween(1, 2)*0.05);
        return;
    }
        
return;
        
#........................................................................................

end


