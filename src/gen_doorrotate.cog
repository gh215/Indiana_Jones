# Jones 3D Cog Script
#
# gen_DoorRotate.cog
#
# Rotating Door Script
#
# [RKD]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols
message	startup		
message	activated	
message	arrived		
message	blocked		

thing		door0
thing		switch

int      open=0				local

flex		duration=1.0

sector	doorSector			local
end

# ========================================================================================

code

startup:
	doorSector = GetThingSector(door0);
	SetSectorAdjoins(doorSector, 0);

	return;

# ........................................................................................

activated:
	if (isMoving(door0)) return;
	if (isMoving(switch)) return;
	if (GetSenderRef() == door0) return;

	MoveToFrame(switch, 1, .5);
	
	if (open == 0)
	{
		PrintInt(open);
		Rotate(door0, 90, 1, duration);
		SetSectorAdjoins(doorSector, 1);	
	}
	else
	{
		PrintInt(open);
		Rotate(door0, -90, 1, duration);
	}
	
   return;

# ........................................................................................

arrived:	

	if (GetSenderRef() == switch)
	{
		if (GetCurFrame(switch)	== 1)
		{
			MoveToFrame(switch, 0, .5);
		}
	}
	else
	{
		open = 1 - open;
		SetSectorAdjoins(doorSector, open);
	}

	return;

# ........................................................................................
end

