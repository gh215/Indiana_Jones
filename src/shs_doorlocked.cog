# Jones 3D Cog Script
#
# SHS_GenDoor.cog      Make doors open/close.  
#
# [JWC, SXC]
#
# (C) 1999 LucasArts Entertainment Company LLC. All Rights Reserved
#
# ========================================================================================

symbols

message activated
message	startup
message entered
message exited

sector 	outsidedoor

thing	door    	   
thing   button       
thing	player			local
thing	interpcam

int  	filter=0 		local		# prevent multiple activates	
int		position=0		local		# door position
int     outside=0       local       # inside or outside

cog     doortalkcog         

end

# ========================================================================================

code


startup:

	player= GetLocalPlayerThing();
    return;

activated:
   
    #  prevent activating with chalk
    if (GetCurItem(player) != 0) return; # prevents activating with chalk

	if (GetsenderRef() == button) 
	{
		position=1;		# door has been opened, no need for won't open lines
	}
	if (position == 1) return;	# door has been opened
    
	if (outside == 0) return;	# makes sure indy is outside of door
	
	if ((GetSenderRef() == door) && (filter == 0))
	{
        #Prep...
		SetActorFlags(player, 0x200000);
		StartCutscene(0);
		StopThing(player);
		DeselectWeapon(player);
		DeselectWeaponWait(player);
		
		# Camera stuff
		SetExtCamOffsetToThing(interpCam);

		filter=1;
        
		PlayMode(player, 60, 1);
       
        # Call DoorTalkCog, use won't open lines
		Sleep(.3);
		SendMessageEx(doortalkcog, user5, 0, 0, 0, 0);
		global15 = 0; # per Randy
		while (global15 == 0)
		{
			Sleep(0.01); # wait for line to finish
		}

        RestoreExtCam();							  
		ClearActorFlags(player, 0x200000);
        EndCutscene();
		filter=0;
	}
    return;

entered:

	if (GetSenderRef() == outsidedoor)
	{
		outside=1;
	}
	return;

exited:
	
	if (GetSenderRef() == outsidedoor)
	{
		outside=0;
	}
	return;

end


