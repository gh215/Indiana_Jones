# Jones 3D Cog Script
#
# Shs_WhipCam.cog
#
# [JWC]
#
# (C) 1999 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols
    
    message     damaged
    message     entered
        
    thing       player      local
    thing       strut       #mask=0x8
    thing       whipCam1
    
    thing     	thisSide
    thing     	thatSide
    
    sound       mus_Cue=mus_gen_indywhip1.wav       local
    
    int         side2=0         local
    int         firstTime=1     local
    
	flex		returncontrol	local

end


# ========================================================================================

code

damaged:

    player = GetLocalPlayerThing();
    
    if(GetParam(1) == 0x10)
    {
        if(firstTime == 1)
        {
            firstTime = 0;
            sleep(1.5);
			PlaySoundLocal(mus_Cue, 1.0, 0.0, 0x0, 0);
        }
        
        # disable controls, switch camera
        StartCutscene(0);
        SetActorFlags(player, 0x200000);
        SetCameraFocus(2, whipCam1);
        SetCameraSecondaryFocus(2, player);
        SetCurrentCamera(2);
        SetCameraFOV(70, 0, 0.0);
    }
                
    return;

# ========================================================================================

entered:
    
    # land on other side
    if((GetSenderRef() == thatSide) && (side2 == 0))
    {
        side2 = 1;
    	call returncontrol;
	}
    
    # land on initial side
    if((GetSenderRef() == thisSide) && (side2 == 1))
    {
        side2 = 0;
        call returncontrol;
	}
    return;
               
# ========================================================================================

returncontrol:

	ClearActorFlags(player, 0x200000);
    EndCutscene();
    SetCameraFOV(90, 0, 0.0);
    SetCurrentCamera(1);
	return;

# ========================================================================================

end


