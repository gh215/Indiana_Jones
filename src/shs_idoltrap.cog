# Jones 3D Cog Script
#
# shs_idoltrap.cog     Floor falls if you take idol.  
#
# [JWC]
#
# (C) 1998 LucasArts Entertainment Company LLC. All Rights Reserved
#
# ========================================================================================

symbols

message startup
message taken 
message	arrived
message entered
message exited
message	crossed
message	pulse

surface	trigger

sound   say_UhOh=Inxj011.wav                local 
sound   sfx_Trap1=pru_trapblock_stuck_c.wav local			  
sound   sfx_Trap2=olv_fallfloor_c.wav		local 				 
sound	in_notfeelright=Inxj043.wav			local # This doesn't feel right.

thing   idol
thing	trapdoor
thing	player								local                       
 
vector		posOffset						local
vector		angOffset						local

int		ontrap=0							local
int 	spoken=0							local
int		vibe=0								local

end


# ========================================================================================

code

startup:
   
	player = GetLocalPlayerThing();
	SetThingLight(trapdoor, '0.2 0.2 0.2', 0.01, 0.01);
	SetThingLight(idol, '0.2 0.2 0.2', 0.01, 0.01);
    return;
		  
taken:
   
	if (GetSenderRef() == idol);
	{
		Sleep(2);
        
		# camera shake
		setpulse(.1);
        
		# move door down slightly to alert player
		PlaySoundLocal(sfx_Trap1, 1.0, 0.0, 0x0, 0);
		MoveToFrame(trapdoor, 1, 2.0);
		
		PlayVoice(player, say_UhOh, 1.0, 1);
		Sleep(0.5);
        Setpulse(0.0);
        
        # lower trap door all the way
		SetActorFlags(player, 0x200000);
        Startcutscene(1);
		MoveToFrame(trapdoor, 2, 8.0);
       
    }
   return;
	 
entered:
	
	if (GetSenderRef() == trapdoor)
	{
	   	ontrap=1;
    	if(GetCurFrame(trapdoor) == 2)
		{
			DamageThing(player, 1000, 0x80, trapdoor);
        }
	}
	return;

exited:
    
	if (GetSenderRef() == trapdoor)
	{
	   	ontrap=0;
    }
	return;

arrived:

	if (GetCurFrame(trapdoor) == 2)
	{
		PlaySoundLocal(sfx_Trap2, 1.0, 0.0, 0x0, 0);
		
		if (ontrap == 1)
		{
			DamageThing(player, 1000, 0x80, trapdoor);
		}
        
		EndCutscene();
		ClearActorFlags(player, 0x200000);
	}
	return;

crossed:
	
	if((GetSenderRef() == trigger) && (spoken == 0))
	{
		PlayVoice(player, in_notfeelright, 1, 0);
		spoken=1;
	}
	return;

# ========================================================================================
			
pulse:
		
		vibe = RandBetween(1, 4);
		if (vibe == 1)
		{
			posOffset = '-0.008 0.00 -0.002'; #1 was .005x
			angOffset = '0.00 -0.002 0.002';
		}
	
		if (vibe == 2)
		{
			posOffset = '0.008 -0.005 0.002'; #2 was .005x
			angOffset = '0.002 0.00 -0.002';
		}
	
		if (vibe == 3)
		{
			posOffset = '0.005 0.005 -0.005'; #3 was .002x
			angOffset = '-0.002 0.005 -0.005';
		}
	
		if (vibe == 4)
		{
			posOffset = '-0.002 0.00 0.005'; #4
			angOffset = '0.00 0.00 0.002';
		}
		
		vibe = RandBetween(1, 3);
		if (vibe == 1)
		{
			SetPulse(0.05);
		}
		if (vibe == 2)
		{
			SetPulse(0.1);
		}
		if (vibe == 3)
		{
			SetPulse(0.15);
		}

		SetPOVShake(posOffSet, angOffSet, 200.0, 200.0);

    	
	return;
			
# ========================================================================================

end


