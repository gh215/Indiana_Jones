# Jones 3D Cog Script
#
# shs_wolf.cog   
#
# [SCHOLL]
#
# (C) 1999 LucasArts Entertainment Company LLC. All Rights Reserved
#
# ========================================================================================
symbols

message entered
message	startup
message	aievent
message	arrivedwpnt
message timer
message user0

thing	t_Wolf
thing	t_Indy		local

int		b_active=0		local
int		n_eventType		local
int		n_CrntAIMode 	local
int		n_OldAIMode 	local
int		n_Waypoint		local
flex	f_DistToIndy	local 
int		n_TimerID		local 
flex	f_FleeTime		local

int		b_OKToStopFlee=0 		local
int		b_AttackOnStopFlee=0 	local
int		n_idx					local
int		b_WpntsActive=0			local
int		n_FleeDir				local

vector	v_IndyPos		local
vector	v_WolfPos		local
vector	v_Temp			local



# ===========================FLEE WAYPOINTS================================================
int			NUM_FLEE_WAYPOINTS=13			local
thing		t_Waypoint00
thing		t_Waypoint01
thing		t_Waypoint02
thing		t_Waypoint03
thing		t_Waypoint04
thing		t_Waypoint05
thing		t_Waypoint06
thing		t_Waypoint07
thing		t_Waypoint08
thing		t_Waypoint09
thing		t_Waypoint10
thing		t_Waypoint11
thing		t_Waypoint12


# ===========================SECTORS================================================

int			NUM_AUTO_FLEE_SECTORS=2 	local
sector		s_AutoFlee00			mask=0x5 # need entered messages from wolf
sector		s_AutoFlee01			mask=0x5 # need entered messages from wolf

int			NUM_ACTIVATE_WPNT_SECTORS=3 local
sector		s_ActivateWpnts00
sector		s_ActivateWpnts01
sector		s_ActivateWpnts02

int			NUM_DEACTIVATE_WPNT_SECTORS=3 local
sector		s_DeactivateWpnts00
sector		s_DeactivateWpnts01
sector		s_DeactivateWpnts02

# ===========================MISC CONSTANTS================================================

flex	MIN_FLEE_DISTANCE=2.0			local # distance wolf wants to be from Indy before stopping fleeing
flex	MIN_FLEE_RESTART_DISTANCE=1.0			local # distance wolf wants to be from Indy before stopping fleeing
flex	MAX_FLEEATTACK_DISTANCE=1.0			local # 
flex	MIN_FLEE_TIME=4.0				local 
flex	VARIABLE_FLEE_TIME=3.0			local

int		TIMER_ID_OKAY_TO_STOP_FLEE=0	local
int		TIMER_ID_START_FLEE=1			local
int		TIMER_ID_ATTACK=2			local

# ===========================SUBROUTINES================================================

flex	SetDistToIndy			local
flex	ActivateWaypoints		local
flex	DeactivateWaypoints		local
flex	RankWaypointsForFlee 	local
flex	RankWaypointsForFleeToEast local
flex 	RankWaypointsForFleeToWest	local
flex	ResetWaypointRanks		local

end
# ========================================================================================

code
startup:
	t_Indy=GetLocalPlayerThing();
    SetThingFlags(t_wolf, 0x80000);

   return;

# ========================================================================================
entered:
	if (GetSourceRef() == t_Indy)
	{
		for (n_idx = 0; n_idx < NUM_ACTIVATE_WPNT_SECTORS; n_idx = n_idx + 1)
		{
			if (GetSenderRef() == s_ActivateWpnts00[n_idx])
			{
				call ActivateWaypoints;
			}
		}
		for (n_idx = 0; n_idx < NUM_DEACTIVATE_WPNT_SECTORS; n_idx = n_idx + 1)
		{
			if (GetSenderRef() == s_DeactivateWpnts00[n_idx])
			{
				call DeactivateWaypoints;
			}
		}
	}
	else if (GetSourceRef() == t_Wolf)
	{
		for (n_idx = 0; n_idx < NUM_AUTO_FLEE_SECTORS; n_idx = n_idx + 1)
		{
			if (GetSenderRef() == s_AutoFlee00[n_idx])
			{
//				DebugPrint("Wolf auto fleeing");
				AIFlee(t_Wolf, t_Indy);
			}
		}
	}

	return;

# ========================================================================================
aievent:
	if(!b_active) return;

	n_eventType		= GetParam(0);
	n_CrntAIMode	= GetParam(1);
	n_OldAIMode		= GetParam(2);	// only valid for certain n_eventType values

	if (n_eventType == 0x100)		// SITHAI_EVENTMODECHANGED
	{
		// if changing to flee mode
		// turn on waypoints
		if ( !BITTEST(n_OldAIMode, 0x800) && BITTEST(n_CrntAIMode, 0x800) )
		{
//			DebugPrint("Entering flee mode");
			AISetSubMode(t_Wolf, 0x8000); // SITHAI_SUBMODECONTINUOUSWPNTMOTION
			f_FleeTime = (MIN_FLEE_TIME + (Rand() * VARIABLE_FLEE_TIME));
			SetTimerEx(f_FleeTime , TIMER_ID_OKAY_TO_STOP_FLEE, 0, 0 );
			b_OKToStopFlee = 0;
			call RankWaypointsForFlee;
		}
		// if exiting flee mode
		// turn off waypoints
		else if ( BITTEST(n_OldAIMode, 0x800) && !BITTEST(n_CrntAIMode, 0x800) )
		{
			call SetDistToIndy;
		
			if (b_AttackOnStopFlee)
			{
				SetTimerEx(0.01 , TIMER_ID_ATTACK, 0, 0);
				b_AttackOnStopFlee = 0;
				AIClearSubMode(t_Wolf, 0x8000); // SITHAI_SUBMODECONTINUOUSWPNTMOTION
				call ResetWaypointRanks;
			}
			// Consider restarting fleeing if Indy too close
			else if (f_DistToIndy < MIN_FLEE_RESTART_DISTANCE)
			{
				// Restart using timer to avoid nested mode changes
				SetTimerEx(0.01 , TIMER_ID_START_FLEE, 0, 0 );
			}
			else
			{
				AIClearSubMode(t_Wolf, 0x8000); // SITHAI_SUBMODECONTINUOUSWPNTMOTION
				call ResetWaypointRanks;
//				DebugPrint("Exiting flee mode");
			}
		}
	}

return;

# ========================================================================================
timer:
	if(!b_active) return;
	t_Indy = GetLocalPlayerThing();

	n_TimerID = GetSenderID();
	if (n_TimerID == TIMER_ID_OKAY_TO_STOP_FLEE)
	{
		b_OKToStopFlee = 1;
	}
	else if (n_TimerID == TIMER_ID_START_FLEE)
	{
		call RankWaypointsForFlee;
		AIFlee(t_Wolf, t_Indy);
	}
	else if (n_TimerID == TIMER_ID_ATTACK)
	{
		AISetFireTarget(t_Wolf, t_Indy);
	}


	return;
# ========================================================================================
 user0:
	if (GetParam(0))
	{
		b_active=1;
		ClearThingFlags(t_wolf, 0x80000);
		call ActivateWaypoints;
	}
	else
	{
		b_active=0;
		SetThingFlags(t_wolf, 0x80000);
	}
	return;

# ========================================================================================
arrivedwpnt:
	if(!b_active) return;
	n_Waypoint = GetParam(0);

	call SetDistToIndy;

	if ( b_OKToStopFlee 
		 && (f_DistToIndy > MIN_FLEE_DISTANCE)
		 && BITTEST(AIGetMode(t_Wolf), 0x800) // fleeing
		 && (RandBetween(0,100) < 90)
		 )
	{
		// StopThing before StopFlee so wolf will turn to face indy
		StopThing(t_Wolf);
		AIStopFlee(t_Wolf);
	}
	else if ( b_OKToStopFlee 
		 && (f_DistToIndy < MAX_FLEEATTACK_DISTANCE)
		 && BITTEST(AIGetMode(t_Wolf), 0x800) // fleeing
		 && (RandBetween(0,100) < 30)
		 )
	{
		b_AttackOnStopFlee = 1;
		AIStopFlee(t_Wolf);
	}
	else if ( (n_FleeDir == -1)
			  && BITTEST(AIGetMode(t_Wolf), 0x800) // fleeing
			  )
	{
		// fleeing west
		if (n_Waypoint <=1)
		{
			if (f_DistToIndy > MIN_FLEE_RESTART_DISTANCE)
			{
				// StopThing before StopFlee so wolf will turn to face indy
//				DebugPrint("Wolf Reached Flee Waypoint and Stopped.");
				StopThing(t_Wolf);
				AIStopFlee(t_Wolf);
			}
			else
			{
				call RankWaypointsForFlee;
			}
		}
	}
	else if ( (n_FleeDir == 1)
			  && BITTEST(AIGetMode(t_Wolf), 0x800) // fleeing
			  )
	{
		// fleeing east
		if (n_Waypoint >=12)
		{
			if (f_DistToIndy > MIN_FLEE_RESTART_DISTANCE)
			{
				// StopThing before StopFlee so wolf will turn to face indy
//				DebugPrint("Wolf Reached Flee Waypoint and Stopped.");
				StopThing(t_Wolf);
				AIStopFlee(t_Wolf);
			}
			else
			{
				call RankWaypointsForFlee;
			}
		}
	}

	return;

# ========================================================================================
SetDistToIndy:
	v_IndyPos = GetThingPos(t_Indy);
	v_WolfPos = GetThingPos(t_Wolf);

	f_DistToIndy = VectorDist(v_IndyPos, v_WolfPos);

	return;

# ========================================================================================
ActivateWaypoints:
	if(!b_active) return;
	if (b_WpntsActive)
	{
		return;
	}

//	DebugPrint("ACTIVATING WAYPOINTS FOR WOLF IN COURTYARD AREA");
	b_WpntsActive = 1;
	AISetInstinctWpntMode(t_Wolf);

	for ( n_idx = 0; n_idx < NUM_FLEE_WAYPOINTS; n_idx = n_idx + 1 )
	{
		AISetWpnt(t_Waypoint00[n_idx], n_idx);
	}

	AIConnectWpnts(0, 1);
	AIConnectWpnts(0, 2);
	AIConnectWpnts(0, 3);
	AIConnectWpnts(1, 2);
	AIConnectWpnts(1, 3);
	AIConnectWpnts(2, 3);
	AIConnectWpnts(2, 5);
	AIConnectWpnts(2, 6);
	AIConnectWpnts(3, 4);
	AIConnectWpnts(3, 5);
	AIConnectWpnts(3, 6);
	AIConnectWpnts(3, 7);
	AIConnectWpnts(4, 6);
	AIConnectWpnts(4, 7);
	AIConnectWpnts(5, 6);
	AIConnectWpnts(5, 8);
	AIConnectWpnts(5, 9);
	AIConnectWpnts(6, 7);
	AIConnectWpnts(6, 8);
	AIConnectWpnts(6, 9);
	AIConnectWpnts(6, 10);
	AIConnectWpnts(7, 9);
	AIConnectWpnts(7, 10);
	AIConnectWpnts(8, 9);
	AIConnectWpnts(9, 10);
	AIConnectWpnts(9, 11);
	AIConnectWpnts(10, 11);
	AIConnectWpnts(10, 12);
	AIConnectWpnts(11, 12);

	return;

# ========================================================================================
DeactivateWaypoints:
	if (!b_WpntsActive)
	{
		return;
	}

//	DebugPrint("DEACTIVATING WAYPOINTS FOR WOLF COURTYARD");
	b_WpntsActive = 0;
	AIClearInstinctWpntMode(t_Wolf);
	StopThing(t_Wolf);

	return;


# ========================================================================================
RankWaypointsForFlee:
	if(!b_active) return;
	v_IndyPos = GetThingPos(t_Indy);
	v_Temp = GetThingPos(t_Waypoint06);

	if (VectorX(v_IndyPos) > VectorX(v_Temp))
	{
		call RankWaypointsForFleeToWest;
	}
	else
	{
		call RankWaypointsForFleeToEast;
	}

	return;

# ========================================================================================
RankWaypointsForFleeToWest:
	
//	DebugPrint("Rank waypoints for Flee To West");
	n_FleeDir = -1;

	AISetWpntRank(0, 135);
	AISetWpntRank(1, 135);

	AISetWpntRank(2, 120);
	AISetWpntRank(3, 120);

	AISetWpntRank(4, 105);
	AISetWpntRank(5, 105);
	AISetWpntRank(6, 105);
	AISetWpntRank(7, 105);

	AISetWpntRank(8, 90);
	AISetWpntRank(9, 90);
	AISetWpntRank(10, 90);

	AISetWpntRank(11, 75);

	AISetWpntRank(12, 60);

	return;

RankWaypointsForFleeToEast:
	
//	DebugPrint("Rank waypoints for Flee To East");
	n_FleeDir = 1;

	AISetWpntRank(0, 30);
	AISetWpntRank(1, 30);
	
	AISetWpntRank(2, 45);

	AISetWpntRank(3, 60);
	AISetWpntRank(4, 60);
	AISetWpntRank(5, 60);
	AISetWpntRank(8, 60);

	AISetWpntRank(6, 75);
	AISetWpntRank(7, 75);
	AISetWpntRank(9, 75);

	AISetWpntRank(10, 90);
	AISetWpntRank(11, 90);
	
	AISetWpntRank(12, 105);

	return;

# ========================================================================================
ResetWaypointRanks:
    if(!b_active) return;
//	DebugPrint("Reset waypoint ranks");

	n_FleeDir = 0;
	for ( n_idx = 0; n_idx < NUM_FLEE_WAYPOINTS; n_idx = n_idx + 1 )
	{
		AISetWpntRank(n_idx, 0);
	}

	return;




end

