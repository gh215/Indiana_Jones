# Jones 3D Cog Script
#
# SOL_Gem2Wall.cog
#
# [TRM]
#
# (C) 1999 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols

    message     damaged
    
    thing       expPos      local
    thing       fragment    local
    thing       t_Spid1
    thing       t_Spid2
    
    surface     front       mask=0x40
    surface     back
    
    cog         hintCog
    
    template    debris0=stoneshrapa         local
    template    debris1=stoneshrapb         local
    template    debris2=stoneshrapc         local
    template    bomb=ghost                  local
    template    tpl_Spider1=spider          local
    template    tpl_Spider2=spider_wall     local
    
    sound       mus_Cue=mus_gen_indyrelief.wav      local
    
    int         i=0         local
    
end

# ========================================================================================
code
      
damaged:

    if(GetSenderRef() == front)
    {
        if(GetParam(1) == 0x4)
        {
            expPos = CreateThingAtPos(bomb, GetSurfaceSector(front), GetSurfaceCenter(front), VectorSet(0, 0, 0));
            for(i=0; i<40; i=i+1)                                                        	            
            {
                fragment = CreateThing(debris0[3.0 * rand()], expPos);                                 
                SetThingVel(fragment, VectorScale(VectorAdd(RandVec(), '-0.5 -0.1 0.0'), 2));
                SetThingRotVel(fragment, VectorScale(VectorAdd(RandVec(), '0.0 0.0 0.0'), 900.0));    	            
            }
            
            SetFaceGeoMode(front, 0);       # no draw
            SetFaceGeoMode(back, 0);
            SetAdjoinFlags(front, 2);       # move
            SetAdjoinFlags(back, 2);
            
            # solve hint23
            SendMessage(hintCog, user0);
            
            Sleep(2.0);
            
            CreateThing(tpl_Spider1, t_Spid1);
            CreateThing(tpl_Spider2, t_Spid2);
            
            # play music cue
            PlaySoundLocal(mus_Cue, 1.0, 0.0, 0x0, 0);
        }
    }
    
    return;        
        
# ========================================================================================

end

