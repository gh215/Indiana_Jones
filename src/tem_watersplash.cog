# Jones 3D Cog Script
#
# TEM_WaterSplash.cog
#
# [TRM]
#
# (C) 1999 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols

    message     entered
    message     timer
    
    thing       origin          # Where the bubbles are coming from
    thing       wave            local
    thing       player          local
    
    sector      sec_on0         linkID=1
    sector      sec_on1         linkID=1
    
    sector      sec_off0        linkID=0
    sector      sec_off1        linkID=0
    
    template    splash0=+ripples2     local
    template    splash1=+ripples      local
    template    splash2=+ripples      local
    
    vector      vecPos          local
    vector      startWave       local
    vector      finWave         local
    
    int         i=0             local
    int		    waves=5		    local
    int         randNum         local
    int         splashing=0     local
    
    flex        fin_XY          local
    
end

# ========================================================================================

code

entered:

    if((GetSenderID() == 1) && (splashing == 0))
    {
        Print("splashing on");
        splashing = 1;
        SetTimer(0.5);
    }
    
    if(GetSenderID() == 0)
    {
        Print("splashing off");
        splashing = 0;
        SetTimer(0.0);
    }

    return;
    
# ========================================================================================

timer:

    vecPos = VectorSet(rand()*0.15, rand()*0.15, 0);
    
    # randomize finish size
    randNum = RandBetween(1, 3);
    if(randNum == 1) fin_XY = 0.1;
    else if(randNum == 2) fin_XY = 0.3;
    else if(randNum == 3) fin_XY = -0.05;
    
    for(i=0; i<waves; i=i+1)                                                        	            
    {                                                                                    	            
        wave = CreateThingAtPos(splash0[RandBetween(0, 2)], GetThingSector(origin), VectorAdd(GetThingPos(origin), VectorAdd('-0.025 -0.035 0.0', vecPos)), '0 0 0');
        CaptureThing(wave);
        
        startWave = VectorSet(0.01, 0.01, 1.0);
        finWave = VectorSet(0.3+fin_XY, 0.3+fin_XY, 0.0); # 0.15
        AnimateSpriteSize(wave, startWave, finWave, 1.3);
    }
    
    
    if(i == 5)
    {
        i = 0;
        SetTimer(0.5);
    }
    
    return;   
    
# ========================================================================================

end

