# Jones 3D Cog Script
#
# Vol_OutsideSecret.cog
#
# Secret Door Script
#
# [CMG]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols
	
#...........MESSAGES..................................	
	message	startup		
	message	activated	
 	

#...........ACTORS..................................	
	thing	player								local

#...........PROPS..................................	
    thing	button		
	thing	door0		
	thing	door1		
    sector	doorSector							local

#...........CAMERAS..................................	
	thing	curCam								local	
    thing	cam1	

#...........CAMERA TARGETS..................................	
    thing	camtarget	


#...........VARIABLES..................................	
    int		open=0								local


#...........SOUNDS..................................	
	sound	in_line1=inxj001b.wav				local	#  ...interesting...
	

#...........KEYFRAMES..................................	
	keyframe	in_free=in_activate_medium.key	local

end

# ========================================================================================

code

startup:
	
	player = GetLocalPlayerThing();
	
	doorSector = GetThingSector(door0);
	SetSectorAdjoins(doorSector, 0);

    return;

# ........................................................................................

activated:

		if ((getSenderRef() == button) && (open == 0)) #button is activated
        {
			open = 1;
        	StartCutScene(1);
            
			# lock out player control
			SetActorFlags(player, 0x200000);
			
			# cutscene camera tricks
			curCam=GetCurrentCamera();
			SetCameraSecondaryFocus(2, button);
			SetCameraFocus(2, cam1);
            Sleep(0.25);
			SetCurrentCamera(2);
			ResetCameraFOV(0, 0.0);
			
			# player activates the button
			PlayMode(player, 60, 0);
			Sleep(0.1);
			MoveToFrame(button, 1, 0.5);
			Sleep(1.0);

			# pan down to the opening doors
			SetCameraInterpSpeed(2, 3);
			SetCameraLookInterp(2,1);
			MoveToFrame(cam1, 1, 1.5);
            Sleep(.5);
			SetCameraSecondaryFocus(2, camtarget);
			
			Sleep(0.5);
			# zoom a little bit
			SetCameraFOV(70, 1, 3);
            
			# clear the sector adjoins, open the doors
			SetSectorAdjoins(doorSector, 1);
            SetPulse(0.1);
			MoveToFrame(door0, 1, 0.5);
			MoveToFrame(door1, 1, 0.5);
            waitforstop(door0);
			SetPulse(0);
            sleep(1.0);
			
			# restore actor
			ClearActorFlags(player, 0x200000);
			
			# restore both cameras
            SetCameraLookInterp(2,0);
			SetCameraFOV(90, 0, 0);
			SetCurrentCamera(curCam);
			SetCameraLookInterp(2,0);
			SetCameraFOV(90, 0, 0);
			EndCutScene();
                
		}
return;
# ========================================================================================
pulse:

	SetPOVShake('0.0 0.0 0.002', '0.0 0.0 0.0', 0.80, 80.0);
return;

# ========================================================================================

		
		
		
end
