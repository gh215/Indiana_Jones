# Jones 3D Cog Script
#
# gen_no_openGATE.cog
#
# Generic Gate Failure Script
#
# [CMG]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols
	

	#..................MESSAGES................
    message		startup		
    message		activated		

	
	#..................ACTORS................
	thing		player          				local
    

	#..................CAMERAS................
    thing       unlockCam          		
	
    
    #..................PROPS................
    thing		gate		
    
	

    #..................SOUNDS................
	
	sound	in_sturdygate=VL04J01.WAV			local # that gate looks pretty sturdy...	
	sound	in_cantopen=Inxj096.wav				local # I can't open it.
	sound	in_nobudge=Inxj098.wav				local # It won't budge.
	sound	in_holdingdoor=Inxj099.wav			local # Something is holding this door closed.
	sound   in_doorislocked=INXJ076.WAV			local # This door is locked.
    sound   in_itslocked=INXJ077.WAV			local # It's locked.
    
	
	
	
	#..................VARIABLES................
    int		curCam								local
	int		curSound							local
	
	int		open=0								local
    int		whipsaid=0							local
    int		gatesaid=0							local
    int		newline								local
    int		oldline								local
	
	flex	randnobudgelines					local

    
end

# ========================================================================================
code

startup:

player = GetLocalPlayerThing();
return;

# ========================================================================================
activated:

If (GetSenderRef() == gate)
	
	{
	   	# Prep for scene...
		MakeMeStop(player);
		StartCutScene(1);
		SetCameraLookInterp(2, 0);
		SetCameraPosInterp(2, 0);
		
		# Slew follow camera to observe the scene from a known position
		SetExtCamOffsetToThing(unlockCam);
		
		DeselectWeaponWait(player); # stow any weapon or lighter
		Sleep(0.25); # wait a beat

        
		# Indy comments on unbreachable barrier...
		if (gatesaid == 0)
		{
			# Indy: "That gate looks pretty sturdy...not sure smash..."
			PlayVoice(player, in_sturdygate, 2, 0);
            gatesaid = 1;
		}
		else
		{
			call randnobudgelines;
		}
		
		# player activates the door
		PlayMode(player, 60, 1);
		Sleep(0.25);
		
        RestoreExtCam();
		ClearActorFlags(player, 0x200000);
		EndCutScene();
	}
return;


# ========================================================================================
randnobudgelines:

	# NOTE: Player-Indy randomly complains about the gate
	# without ever saying the same line twice in a row
	
	while (newline == oldline)
	{	
		newline = RandBetween(1, 3); # pick new line
	}
	oldline = newline; # remember what got picked

	if (newline == 1)
	{
		curSound = PlayVoice(player, in_cantopen, 1.0, 0);
	}	
	if (newline == 2)
	{
		curSound = PlayVoice(player, in_nobudge, 1.0, 0);
	}
	if (newline == 3)
	{
		curSound = PlayVoice(player, in_holdingdoor, 1.0, 0);
	}

	return;	

# ========================================================================================

end

