# Jones 3D Cog Script
#
# SHS_Snow.cog   Makes it snow.
# 
#
# [JWC]
#
# (C) 1999 LucasArts Entertainment Co. All Rights Reserved

symbols
	message	startup
    message crossed
	message	pulse
	
    surface on		linkid=1
    surface off		linkid=2
	surface	on1		linkid=1
	surface off1	linkid=2


	thing	ghost0
              
    template	particle
	
    int	        dummy       local
	int         crosschk=0  local            
	int 		player		local

    flex		pulsetime=.5
    
end

code

startup:
	player=GetLocalPlayerThing();
	return;

crossed:
	

	# make sure Indy player is sending message
	If (GetSourceRef() != player) return;

	# turn snow on
	If ((crosschk == 0)  && (GetSenderId() == 1))
    {
        Print("snow on");
		crosschk = 1;
        setpulse(pulsetime);
	}    
    
	# turn snow off
	If ((crosschk == 1) && (GetSenderId() == 2))
    {
        Print("snow off");
		crosschk=0;
        setpulse(0);
	}
    return;

pulse:
	dummy=CreateThing(particle, ghost0);
	return;
	

end